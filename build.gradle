plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'lordyorden'
version = '1.0'
description = 'cloud-demo'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

bootBuildImage {
    // Default name if no override is provided in the command line

    def name = project.hasProperty('group') ? project.property('group').toString() : "lordyorden"

    imageName = "ghcr.io/${name}/${project.name.toLowerCase()}:${version}"

    // Controlled by the -P publish=true flag in the GitHub Action
    def shouldPublish = project.hasProperty('publish') ? project.property('publish').toBoolean() : false
    publish = shouldPublish


    if(shouldPublish) {
        environment = [
                "BP_JVM_VERSION": "25",
                "BP_JVM_ID": "temurin"
        ]

        docker {
            publishRegistry {
                url = "https://ghcr.io"
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'

    // https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0"

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-mongodb-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
